package BountyController

import Table
import LinkedList
import ClosureTimers
import ErrorHandling
import Annotations

//This constants can be edited (obviously only valid values)
@configurable constant string DEF_COLOR_GOLD = "|cffffcc00"
@configurable constant string DEF_COLOR_LUMBER = "|cff32cd32"
@configurable constant real DEF_SIZE = 10.00
@configurable constant real DEF_LIFE_SPAN = 3.50
@configurable constant real DEF_AGE = 0.00
@configurable constant real DEF_SPEED = 64
@configurable constant angle DEF_DIRECTION = 90 .asAngleDegrees()
@configurable constant real DEF_FADE_POINT = 2.50
@configurable constant string DEF_STATE = "gold" // or "lumber"
@configurable constant real DEF_HEIGHT = 0
@configurable constant boolean DEF_SHOW = true
@configurable constant boolean DEF_SHOW_NOTHING = false
@configurable constant boolean DEF_ALLOW_FRIEND_FIRE = false
@configurable constant string DEF_EFFECT = "UI\\Feedback\\GoldCredit\\GoldCredit.mdl"
@configurable constant boolean DEF_SHOW_EFFECT = true
@configurable constant boolean DEF_PERMANENT = false
@configurable constant integer LIMIT_RECURSION = 16 // If a loop caused by recursion is doing in porpouse you can edit the tolerance of how many calls can do

// End of configuration

/** To register and broadcast bounty events*/
public abstract class BountyListener
    protected int index
    protected LinkedList<BountyListener> list

    abstract function run(Bounty bounty)

    function disconnect()
        list.remove(this)

    function reconnect()
        list.addAt(this, index)

@deprecated("Use Bounty.onRun instead")
public function registerBountyEvent(BountyListener cb) returns BountyListener
    return Bounty.onRun(cb)

@deprecated("Use Bounty.onDead instead")
public function registerBountyDeadEvent(BountyListener cb) returns BountyListener
    return Bounty.onDead(cb)

public class Bounty
    protected static trigger controller

    private static Table bounties0 = new Table()
    private static Table bounties1 = new Table()
    private static Table bounties2 = new Table()

    private static int recursion
    private static constant SPEED_COEF = 0.071 / 128

    private static LinkedList<BountyListener> onRun = new LinkedList<BountyListener>()
    private static LinkedList<BountyListener> onDead = new LinkedList<BountyListener>()

    private int amount
    private real size
    private real lifeSpan
    private real age
    private real speed
    private angle direction
    private real fadePoint
    private string state
    private real height
    private boolean show
    private boolean showNothing
    private boolean allowFriendFire
    private string eff
    private boolean showEff
    private boolean permanent
    private player receiver
    private unit unitPos
    private texttag textTag

    private unit killingUnit
    private unit dyingUnit
    
    private vec2 pos
    private string color
    private boolean isLoc
    private Table whoSee

    construct()
        recursion++
        amount = 0
        size = DEF_SIZE
        lifeSpan = DEF_LIFE_SPAN
        age = DEF_AGE
        speed = DEF_SPEED
        direction = DEF_DIRECTION
        fadePoint = DEF_FADE_POINT
        state = DEF_STATE
        height = DEF_HEIGHT
        show = DEF_SHOW
        showNothing = DEF_SHOW_NOTHING
        eff = DEF_EFFECT
        showEff = DEF_SHOW_EFFECT
        permanent = DEF_PERMANENT
        isLoc = false
        pos = ZERO2
        color = null
        whoSee = new Table()

    ondestroy
        recursion--
        receiver = null
        unitPos = null
        killingUnit = null
        dyingUnit = null
        destroy whoSee
        whoSee = null

    //The functions to set the bounty stats

    static function setBase(integer id, integer b)
        bounties0.saveInt(id, b)

    static function setDice(integer id, integer d)
        bounties1.saveInt(id, d)

    static function setSide(integer id, integer s)
        bounties2.saveInt(id, s)

    /** Set the received bounty when the unit is killed*/
    static function set(integer id, integer b, integer d, integer s)
        bounties0.saveInt(id, b)
        bounties1.saveInt(id, d)
        bounties2.saveInt(id, s)

    //The functions to get the bounty stats (that you set before)

    static function getBase(integer id) returns integer
        return bounties0.loadInt(id)

    static function getDice(integer id) returns integer
        return bounties1.loadInt(id)

    static function getSide(integer id) returns integer
        return bounties2.loadInt(id)

    /** Get the bounty the you would get if the unit is killed (You should set it before)*/
    static function get(integer id) returns integer
        return getBase(id) + GetRandomInt(0, getDice(id) * getSide(id))

    protected static function setData()
        /*Peasant*/ set('hpea', 15, 5, 3)

        for i = 0 to bj_MAX_PLAYER_SLOTS
            players[i].setState(PLAYER_STATE_GIVES_BOUNTY, 0)
    
    /** Enables the death trigger*/
    static function enable()
        controller.enable()
    
    /** Disables the death trigger*/
    static function disable()
        controller.disable()
    
    /** Add a callback that runs when a unit kills another*/
    static function onDead(BountyListener cb) returns BountyListener
        onDead.add(cb)
        cb.list = onDead
        cb.index = onDead.indexOf(cb)
        return cb
    
    /** Add a callback that runs a bounty was runned*/
    static function onRun(BountyListener cb) returns BountyListener
        onRun.add(cb)
        cb.list = onRun
        cb.index = onRun.indexOf(cb)
        return cb
    
    /** Set if the player can see the bounty*/
    function canSee(player p, boolean flag)
        whoSee.saveBoolean(p.getId(), flag)

    /** Returns if the player can see the bounty*/
    function isSeeing(player p) returns boolean
        return whoSee.loadBoolean(p.getId())

    private static function sign(int i) returns string
        if i < 0
            return ""
        return "+"

    /** Run the bounty and get the texttag*/
    function run() returns texttag
        playerstate what = null
        texttag tt

        if recursion > LIMIT_RECURSION //If there is recursion that don't stop soon, the system stops automatically
            error("There is a recursion with the Bounty Controller, check if you are not creating a infinite loop.")
        
        if state == "gold"
            what = PLAYER_STATE_RESOURCE_GOLD
        else if state == "lumber"
            what = PLAYER_STATE_RESOURCE_LUMBER
        else
            error("Not valid state")

        if amount == 0 and not showNothing
            show = false
            showEff = false

        receiver.addState(what, amount)

        if color == null
            if state == "gold"
                color = DEF_COLOR_GOLD
            else if state == "lumber"
                color = DEF_COLOR_LUMBER
        
        if not isLoc
            if unitPos != null
                pos = unitPos.getPos()
            else
                show = false
                showEff = false // If there is no position to the text, the text and the effect won't show

        speed *= SPEED_COEF
        
        textTag = CreateTextTag()
            ..setPermanent(permanent)
            ..setText(color + sign(amount) + amount.toString() + "|r", size)
            ..setVisibility(show and isSeeing(localPlayer))
            ..setPos(pos.x, pos.y, height)
            ..setFadepoint(fadePoint)
            ..setLifespan(lifeSpan)
            ..setVelocity(speed * direction.cos(), speed * direction.sin())
            ..setAge(age)

        if showEff
            flashEffect(eff, pos)
        
        for cb in onRun
            cb.run(this)

        tt = textTag

        destroy this

        return tt

    /** Run the bounty and get the texttag*/
    function run(integer bounty, unit pos, player myplayer, boolean addplayer, boolean perm) returns texttag
        amount = bounty
        unitPos = pos
        receiver = myplayer
        canSee(myplayer, addplayer)
        permanent = perm
        return run()

    /** Change the player who will get the bounty and set if the new and/or old can see it*/
    function changeReceiver(player newPlayer, boolean removePrevious, boolean addNew)
        canSee(newPlayer, not removePrevious)
        receiver = newPlayer
        canSee(newPlayer, addNew)

    function setColor(string color)
        this.color = color

    function setColor(color color)
        this.color = color.toColorString()

    function setColor(colorA color)
        this.color = color.withoutAlpha().toColorString()

    function getColor() returns string
        return color

    /** Set the position of the bounty (this has more priority than unitPos)*/
    function setPos(vec2 pos)
        this.pos = pos
        isLoc = true
    
    function getPos() returns vec2
        if not isLoc
            return unitPos.getPos()
        return pos

    function getAmount() returns int
        return amount

    function setAmount(int amount)
        this.amount = amount

    function getSize() returns real
        return size

    function setSize(real size)
        this.size = size

    function getLifeSpan() returns real
        return lifeSpan

    function setLifeSpan(real lifeSpan)
        this.lifeSpan = lifeSpan

    function getAge() returns real
        return age

    function setAge(real age)
        this.age = age

    function getSpeed() returns real
        return speed

    function setSpeed(real speed)
        this.speed = speed

    function getDirection() returns angle
        return direction

    @deprecated("Pass an angle instead")
    function setDirection(real direction)
        this.direction = direction.asAngleDegrees()

    function setDirection(angle direction)
        this.direction = direction

    function getFadePoint() returns real
        return fadePoint

    function setFadePoint(real fadePoint)
        this.fadePoint = fadePoint

    function getState() returns string
        return state

    function setState(string state)
        this.state = state

    function getHeight() returns real
        return height

    function setHeight(real height)
        this.height = height

    function showing() returns boolean
        return show

    function setShow(boolean show)
        this.show = show

    /** Get if the bounty appears even if is 0*/
    function showingNothing() returns boolean
        return showNothing

    /** Set if the bounty appears even if is 0*/
    function setShowNothing(boolean showNothing)
        this.showNothing = showNothing

    function allowingFriendFire() returns boolean
        return allowFriendFire

    function setAllowFriendFire(boolean allowFriendFire)
        this.allowFriendFire = allowFriendFire

    function getEff() returns string
        return eff

    function setEff(string eff)
        this.eff = eff

    function showingEff() returns boolean
        return showEff

    function setShowEff(boolean showEff)
        this.showEff = showEff

    function isPermanent() returns boolean
        return permanent

    function setPermanent(boolean permanent)
        this.permanent = permanent

    function getReceiver() returns player
        return receiver

    function setReceiver(player receiver, boolean see)
        this.receiver = receiver
        canSee(receiver, see)

    function getUnitPos() returns unit
        return unitPos

    function setUnitPos(unit unitPos)
        this.unitPos = unitPos

    function getTextTag() returns texttag
        return textTag

    function getKillingUnit() returns unit
        return killingUnit

    function getDyingUnit() returns unit
        return dyingUnit
    
    protected static function actions()
        let curr = new Bounty()
        curr.killingUnit = GetKillingUnit()
        curr.dyingUnit = GetDyingUnit()
        curr.amount = Bounty.get(curr.dyingUnit.getTypeId()) 
    
        curr.receiver = curr.killingUnit.getOwner()
        curr.canSee(curr.receiver, true)
        curr.unitPos = curr.dyingUnit

        //Run event
        for cb in onDead
            cb.run(curr)
    
        if IsUnitEnemy(curr.dyingUnit, curr.receiver) or curr.allowFriendFire
            curr.run()
        else
            destroy curr

init
    nullTimer(() -> Bounty.setData())
    Bounty.controller = CreateTrigger()
        ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_DEATH)
        ..addAction(function Bounty.actions)
